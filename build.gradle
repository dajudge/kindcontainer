plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "com.palantir.git-version" version "0.15.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

def versionDetails = versionDetails()
version versionDetails.isCleanTag && gitVersion().startsWith("release/")
        ? gitVersion().substring("release/".length())
        : System.currentTimeMillis() + "-SNAPSHOT"
println "Build version $version"

apply from: "${rootProject.projectDir}/publishing.gradle"
apply from: "${rootProject.projectDir}/signing.gradle"

dependencies {
    api 'org.testcontainers:testcontainers:1.17.1'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    testImplementation 'io.fabric8:kubernetes-client:5.12.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'org.mockito:mockito-core:4.4.0'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.11'
}

java {
    withJavadocJar()
    withSourcesJar()
}


test {
    // suppress completely stupid stacktrace dump by fabric8
    systemProperty "kubernetes.disable.autoConfig", "true"

    environment 'TESTCONTAINERS_REUSE_ENABLE', 'true'

    testLogging {
        showStandardStreams = true
    }
}
