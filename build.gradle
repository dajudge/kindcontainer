plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "com.palantir.git-version" version "0.12.2"
    id "com.github.hierynomus.license" version "0.16.1"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

def versionDetails = versionDetails()
version versionDetails.isCleanTag && gitVersion().startsWith("release/")
        ? gitVersion().substring("release/".length())
        : System.currentTimeMillis() + "-SNAPSHOT"
println "Build version $version"

apply from: "${rootProject.projectDir}/publishing.gradle"
apply from: "${rootProject.projectDir}/signing.gradle"

dependencies {
    api 'io.fabric8:kubernetes-client:5.11.2'
    api 'org.testcontainers:testcontainers:1.16.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.10'
}

java {
    withJavadocJar()
    withSourcesJar()
}


test {
    // suppress completely stupid stacktrace dump by fabric8
    systemProperty "kubernetes.disable.autoConfig", "true"


    testLogging {
        showStandardStreams = true
    }
}

license {
    header file("${project.rootDir}/HEADER")
}
