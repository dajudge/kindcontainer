plugins {
    id "java-library"
    id "com.palantir.git-version" version "1.0.0"
    id "com.vanniktech.maven.publish" version "0.35.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

def versionDetails = versionDetails()
version versionDetails.isCleanTag && gitVersion().startsWith("release/")
        ? gitVersion().substring("release/".length())
        : System.currentTimeMillis() + "-SNAPSHOT"
println "Build version $version"

apply from: "${rootProject.projectDir}/publishing.gradle"
apply from: "${rootProject.projectDir}/codegen.gradle"

ext {
    testcontainersVersion = "2.0.1"
    junitVersion = "5.11.4"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    api "org.testcontainers:testcontainers:$testcontainersVersion"
    implementation "org.bouncycastle:bcpkix-jdk18on:1.83"

    testImplementation "org.testcontainers:testcontainers-junit-jupiter:$testcontainersVersion"
    testImplementation "io.fabric8:kubernetes-client:7.4.0"
    testImplementation "org.awaitility:awaitility:4.3.0"
    testImplementation "org.mockito:mockito-core:5.20.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation 'com.sparkjava:spark-core:2.9.4'
    testImplementation "org.slf4j:slf4j-api:2.0.17"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:6.0.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.5.19"
}

test {
    // suppress completely stupid stacktrace dump by fabric8
    systemProperty "kubernetes.disable.autoConfig", "true"

    environment 'TESTCONTAINERS_REUSE_ENABLE', 'true'
    
    if(System.getenv("CONTAINER_FILTER")) {
        environment 'CONTAINER_FILTER', System.getenv("CONTAINER_FILTER")
    }

    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
    }
}
